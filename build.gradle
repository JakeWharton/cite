import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin

buildscript {
	dependencies {
		classpath libs.kotlin.gradlePlugin
		classpath libs.buildConfigPlugin
		classpath libs.gradleMavenPublishPlugin
		classpath libs.dokkaPlugin
		classpath 'com.jakewharton.cite:cite-gradle-plugin'
	}
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
}

allprojects {
	version = property("VERSION_NAME") as String

	repositories {
		mavenCentral()
	}

	configurations.configureEach {
		resolutionStrategy.dependencySubstitution {
			substitute(module("com.jakewharton.cite:cite-kotlin-plugin"))
				.using(project(":cite-kotlin-plugin"))
				.because("In-repo sample usage")
		}
	}

  tasks.withType(JavaCompile).configureEach { task ->
    task.sourceCompatibility = '1.8'
    task.targetCompatibility = '1.8'
  }

  tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile).configureEach { task ->
    task.kotlinOptions {
      jvmTarget = '1.8'
    }
  }

	plugins.withId('maven-publish') {
		publishing {
			repositories {
				maven {
					name = "TestMaven"
					url = file("${rootProject.buildDir}/testMaven").toURI().toString()
				}
			}
		}
	}
}
