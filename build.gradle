import org.jetbrains.kotlin.gradle.targets.js.nodejs.NodeJsRootPlugin

buildscript {
	dependencies {
		classpath libs.kotlin.gradlePlugin
		classpath libs.buildConfigPlugin
		classpath libs.gradleMavenPublishPlugin
		classpath libs.dokkaPlugin
		classpath 'com.jakewharton.cite:cite-gradle-plugin'
	}
	repositories {
		mavenCentral()
		gradlePluginPortal()
	}
}

plugins.withType(NodeJsRootPlugin).configureEach {
	kotlinNodeJs {
		nodeVersion = '20.0.0'
	}
}

allprojects {
	version = property("VERSION_NAME") as String

	repositories {
		mavenCentral()
	}

	configurations.configureEach {
		resolutionStrategy.dependencySubstitution {
			substitute(module("com.jakewharton.cite:cite-kotlin-plugin"))
				.using(project(":cite-kotlin-plugin"))
				.because("In-repo sample usage")
		}
	}

	plugins.withId('java-base') {
		java {
			sourceCompatibility = JavaVersion.VERSION_1_8
			targetCompatibility = JavaVersion.VERSION_1_8
		}
	}

	plugins.withId('org.jetbrains.kotlin.jvm') {
		kotlin {
			jvmToolchain {
				languageVersion.set(JavaLanguageVersion.of(8))
			}
		}
	}

	plugins.withId('org.jetbrains.kotlin.multiplatform') {
		kotlin {
			jvmToolchain {
				languageVersion.set(JavaLanguageVersion.of(8))
			}
		}
	}

	plugins.withId('maven-publish') {
		publishing {
			repositories {
				maven {
					name = "TestMaven"
					url = file("${rootProject.buildDir}/testMaven").toURI().toString()
				}
			}
		}
	}
}
