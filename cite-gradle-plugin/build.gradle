apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'java-gradle-plugin'
apply plugin: 'com.github.gmazzo.buildconfig'

gradlePlugin {
	plugins {
		cite {
			id = 'com.jakewharton.cite'
			displayName = "Cite"
			description = "Cite compiler plugin"
			implementationClass = "com.jakewharton.cite.plugin.gradle.CitePlugin"
		}
	}
}

buildConfig {
	useKotlinOutput {
		internalVisibility = true
		topLevelConstants = true
	}

	packageName('com.jakewharton.cite.plugin.gradle')
	buildConfigField("String", "CiteVersion", "\"$version\"")
}

kotlin {
	explicitApi()
}

dependencies {
	compileOnly libs.kotlin.gradlePlugin

	testImplementation gradleTestKit()
	testImplementation libs.junit
	testImplementation libs.assertk
	testImplementation libs.asm.util
}

if (rootProject.name == 'cite') {
	apply plugin: 'com.vanniktech.maven.publish'

	test {
		dependsOn(':cite-gradle-plugin:publishAllPublicationsToTestMavenRepository')
		dependsOn(':cite-kotlin-plugin:publishAllPublicationsToTestMavenRepository')
		dependsOn(':cite-runtime:publishAllPublicationsToTestMavenRepository')

		inputs.dir(file('src/test/fixtures'))
	}
}
